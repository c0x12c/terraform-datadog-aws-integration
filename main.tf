data "aws_caller_identity" "this" {}

data "aws_iam_policy_document" "datadog_aws_integration_assume_role" {
  statement {
    actions = ["sts:AssumeRole"]

    principals {
      type = "AWS"
      identifiers = ["arn:aws:iam::${local.datadog_aws_account_id}:root"]
    }

    condition {
      test     = "StringEquals"
      variable = "sts:ExternalId"

      values = [
        "${datadog_integration_aws_account.sandbox.auth_config.aws_auth_config_role.external_id}"
      ]
    }
  }
}

data "aws_iam_policy_document" "datadog_aws_integration" {
  statement {
    actions = var.datadog_permissions == null ? local.datadog_permissions : var.datadog_permissions
    resources = ["*"]
  }
}

resource "aws_iam_policy" "datadog_aws_integration" {
  name   = "DatadogAWSIntegrationPolicy"
  policy = data.aws_iam_policy_document.datadog_aws_integration.json
}

resource "aws_iam_role" "datadog_aws_integration" {
  name               = var.datadog_aws_integration_iam_role
  description        = "Role for Datadog AWS Integration"
  assume_role_policy = data.aws_iam_policy_document.datadog_aws_integration_assume_role.json
}

resource "aws_iam_role_policy_attachment" "datadog_aws_integration" {
  role       = aws_iam_role.datadog_aws_integration.name
  policy_arn = aws_iam_policy.datadog_aws_integration.arn
}

/**
Create and manage Datadog - Amazon Web Services integration.
version = "~> 3.51.0"
https://registry.terraform.io/providers/DataDog/datadog/latest/docs/resources/integration_aws_account
 */
resource "datadog_integration_aws_account" "sandbox" {
  aws_account_id = data.aws_caller_identity.this.account_id
  aws_partition  = "aws"

  aws_regions {
    include_all = true
  }

  auth_config {
    aws_auth_config_role {
      role_name = var.datadog_aws_integration_iam_role
    }
  }

  logs_config {
    lambda_forwarder {}
  }

  metrics_config {
    collect_custom_metrics = true

    namespace_filters {
      include_only = local.enabled_aws_service_names
    }
  }

  resources_config {
    cloud_security_posture_management_collection = true
    extended_collection                          = true
  }

  traces_config {
    xray_services {}
  }
}
